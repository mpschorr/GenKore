import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.22'
    id 'maven-publish'
}

group = 'xyz.jeelzzz'
version = '0.0.1'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'triumph'
        url = "https://repo.triumphteam.dev/snapshots/"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT'

    compileOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.0'
    compileOnly 'com.fasterxml.jackson.module:jackson-module-kotlin:2.17.0'

    implementation 'dev.triumphteam:triumph-gui:3.1.7'
    implementation 'dev.triumphteam:triumph-cmd-bukkit:2.0.0-SNAPSHOT'
}

shadowJar {
    exclude 'org/jetbrains/**'
    exclude 'org/intellij/**'
    exclude 'kotlin/**'
    exclude 'net/kyori/**'
    exclude 'com/google/gson/**'
//    exclude 'org/yaml/snakeyaml/**'

    relocate('dev.triumphteam.cmd', 'xyz.jeelzzz.genkore.lib.cmd')
    relocate('dev.triumphteam.gui', 'xyz.jeelzzz.genkore.lib.gui')
}

compileKotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            name = "jeelzzzRepository"
            url = "https://repo.jeelzzz.xyz/snapshots"
            credentials(org.gradle.api.credentials.PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = version
            from components.java
        }
    }
}

build {
    dependsOn 'shadowJar'
}